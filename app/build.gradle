plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id "io.gitlab.arturbosch.detekt" version "1.22.0"
}

android {
    namespace 'cz.cvut.fukalhan.swap'
    compileSdk 33

    defaultConfig {
        applicationId "cz.cvut.fukalhan.swap"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        def apikeyPropertiesFile = rootProject.file("${project.rootDir}/apikey.properties")
        def apikeyProperties = new Properties()
        apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))
        buildConfigField("String", "STREAM_CHAT_API_KEY", "\"${apikeyProperties['STREAM_CHAT_API_KEY']}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(':feature:login')
    implementation project(':feature:profile')
    implementation project(':feature:profile-detail')
    implementation project(':feature:add-item')
    implementation project(':feature:item-list')
    implementation project(':feature:item-detail')
    implementation project(':feature:settings')
    implementation project(':feature:messages')
    implementation project(':feature:notifications')
    implementation project(':feature:add-review')
    implementation project(':feature:events')
    implementation project(':library:auth')
    implementation project(':library:design')
    implementation project(':library:user-data')
    implementation project(':library:item-data')
    implementation project(':library:events-data')

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'

    // Compose
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"

    // Koin
    implementation "io.insert-koin:koin-android:3.3.3"
    implementation "io.insert-koin:koin-core:3.3.3"
    implementation "io.insert-koin:koin-androidx-compose:3.4.2"

    // Handles all Firebase libraries' versions
    implementation platform('com.google.firebase:firebase-bom:31.3.0')
    implementation 'com.google.firebase:firebase-auth-ktx'

    // Stream Chat
    implementation "io.getstream:stream-chat-android-client:$stream_version"
    implementation "io.getstream:stream-chat-android-offline:$stream_version"
    implementation "io.getstream:stream-chat-android-pushprovider-firebase:$stream_version"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.22.0"
}

detekt {
    toolVersion = "1.22.0"
    source = files("src/main/kotlin")
    config = files("${project.rootDir}/detekt.yml")
    buildUponDefaultConfig = true
}